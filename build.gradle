
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.3.0'
  }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	apply plugin: "jacoco"
	apply plugin: 'coveralls'

    group = 'com.github.fabito'
    version = '0.7'

	def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath

	repositories {
	    mavenCentral()
	    mavenRepo url: localMavenRepo
	}

	dependencies {
	    compile "commons-configuration:commons-configuration:1.10"
	    compile "javax.inject:javax.inject:1"
	    
	    testCompile group: 'junit', name: 'junit', version: '4.+'
	    testCompile 'org.mockito:mockito-all:1.9.5'
	    testCompile 'com.google.guava:guava:16.0.1'
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}

	jacocoTestReport {
	    reports {
	        xml.enabled = true // coveralls plugin depends on xml format report
	        html.enabled = true
	    }
	}

	uploadArchives {
	    repositories {
	        mavenDeployer {
	            repository(url: "file://localhost/home/fabio/Development/eclipse-workspaces/oss/mvn-repo/releases")
	            snapshotRepository(url: "file://localhost/home/fabio/Development/eclipse-workspaces/oss/mvn-repo/snapshots")
	        }
	    }
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
